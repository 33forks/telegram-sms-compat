apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 29

    defaultConfig {
        applicationId "com.qwe7002.telegram_sms_compat"
        minSdkVersion 16
        //noinspection ExpiredTargetSdkVersion
        targetSdkVersion 21
        versionCode System.getenv("VERSION_CODE") == null ? 1 : System.getenv("VERSION_CODE").toInteger()
        versionName System.getenv("VERSION_NAME") == null ? "debug-Do_not_leak" : System.getenv("VERSION_NAME").toString()
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        releaseConfig {
            storeFile file("keys.jks")
            storePassword project.hasProperty("KEYSTORE_PASS") ? KEYSTORE_PASS : System.getenv("KEYSTORE_PASS")
            keyAlias project.hasProperty("ALIAS_NAME") ? ALIAS_NAME : System.getenv("ALIAS_NAME")
            keyPassword project.hasProperty("ALIAS_PASS") ? ALIAS_PASS : System.getenv("ALIAS_PASS")
            v1SigningEnabled true
            v2SigningEnabled true
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.releaseConfig
            ndk {
                abiFilters "armeabi-v7a", "x86"
            }
        }
        debug {
            minifyEnabled false
            ndk {
                abiFilters "armeabi-v7a", "x86"
            }
        }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.1'
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'com.squareup.okio:okio:2.8.0'
    //Okhttp compatible stop at 3.12.x
    //noinspection GradleDynamicVersion,GradleDependency
    implementation "com.squareup.okhttp3:okhttp:3.12.+"
    //noinspection GradleDynamicVersion,GradleDependency
    implementation "com.squareup.okhttp3:okhttp-dnsoverhttps:3.12.+"
    implementation 'org.conscrypt:conscrypt-android:2.5.1'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'io.paperdb:paperdb:2.7.1'
    implementation 'me.dm7.barcodescanner:zxing:1.9.13'
    implementation 'androidx.core:core-ktx:1.3.1'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.4.10"
}
repositories {
    mavenCentral()
}

task copy_language_pack(type: Copy) {
    description 'Copy the language pack to the source directory, replace the placeholder file'
    from 'language_pack/'
    into 'src/main/res/'
    exclude '**/README.md'
    exclude '**/crowdin.yml'
    include '**/*'
}
task pull_language_pack(type: Exec) {
    description 'Pull remote language pack'
    workingDir 'language_pack/'
    commandLine "git", "pull"
}